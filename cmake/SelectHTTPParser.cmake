# Optional external dependency: http-parser
add_library(http_parser INTERFACE)

if(USE_HTTP_PARSER STREQUAL "http-parser")
	find_package(HTTPParser)

	if(HTTP_PARSER_FOUND AND HTTP_PARSER_VERSION_MAJOR EQUAL 2)
		target_include_directories(http_parser INTERFACE "${HTTP_PARSER_INCLUDE_DIRS}")
		target_link_libraries(http_parser INTERFACE ${HTTP_PARSER_LIBRARIES})
		list(APPEND LIBGIT2_PC_LIBS "-lhttp_parser")
		set(GIT_HTTPPARSER_HTTPPARSER 1)
		add_feature_info(http-parser ON "using http-parser (system)")
	else()
		message(FATAL_ERROR "http-parser support was requested but not found")
	endif()
elseif(USE_HTTP_PARSER STREQUAL "llhttp")
	find_package(LLHTTP)

	if(LLHTTP_FOUND AND LLHTTP_VERSION_MAJOR EQUAL 9)
		target_link_libraries(http_parser INTERFACE llhttp)
		list(APPEND LIBGIT2_PC_LIBS "-lllhttp")
		set(GIT_HTTPPARSER_LLHTTP 1)
		add_feature_info(http-parser ON "using llhttp (system)")
	else()
		message(FATAL_ERROR "llhttp support was requested but not found")
	endif()
else()
	add_subdirectory("${PROJECT_SOURCE_DIR}/deps/llhttp" "${PROJECT_BINARY_DIR}/deps/llhttp")
	target_link_libraries(http_parser INTERFACE llhttp)
	set(GIT_HTTPPARSER_BUILTIN 1)
	add_feature_info(http-parser ON "using bundled parser")
endif()
